model {
    for (i in 1:N) {
        for (j in 1:9) {
            y[i, j] ~ dnorm(mu[i, j], psi[j])
        }
        mu[i, 1] <- u[1] + a[1] * c[i] + eta[i]
        mu[i, 2] <- u[2] + a[2] * c[i] + lam[1] * eta[i]
        mu[i, 3] <- u[3] + a[3] * c[i] + lam[2] * eta[i]
        mu[i, 4] <- u[4] + a[4] * c[i] + xi[i, 1]
        mu[i, 5] <- u[5] + a[5] * c[i] + lam[3] * xi[i, 1]
        mu[i, 6] <- u[6] + a[6] * c[i] + lam[4] * xi[i, 1]
        mu[i, 7] <- u[7] + a[7] * c[i] + xi[i, 2]
        mu[i, 8] <- u[8] + a[8] * c[i] + lam[5] * xi[i, 2]
        mu[i, 9] <- u[9] + a[9] * c[i] + lam[6] * xi[i, 2]

        # structural equation
        eta[i] ~ dnorm(nu[i], psd)

        nu[i] <- b * d[i] + gam[1] * xi[i, 1] + gam[2] * xi[i, 2] + t * gam[3] * xi[i, 1] * xi[i, 2] + t * gam[4] * xi[i, 2] * xi[i, 2]

        uu[i] <- (eta[i] - nu[i]) * psd * (gam[3] * xi[i, 1] * xi[i, 2] + gam[4] * xi[i, 2] * xi[i, 2])

        xi[i, 1:2] ~ dmnorm(zero[1:2], phi[1:2, 1:2])

    }  # end of i

    ubar <- sum(uu[])

    # prior distribution
    lam[1] ~ dnorm(0.5, psi[2])
    lam[2] ~ dnorm(0.5, psi[3])
    lam[3] ~ dnorm(0.5, psi[5])
    lam[4] ~ dnorm(0.5, psi[6])
    lam[5] ~ dnorm(0.5, psi[8])
    lam[6] ~ dnorm(0.5, psi[9])

    b ~ dnorm(0.5, psd)
    gam[1] ~ dnorm(0.4, psd)
    gam[2] ~ dnorm(0.3, psd)
    gam[3] ~ dnorm(0.5, psd)
    gam[4] ~ dnorm(0.5, psd)

    for (j in 1:9) {
        psi[j] ~ dgamma(9, 4)
        sgm[j] <- 1/psi[j]
        u[j] ~ dnorm(0, 1)
        a[j] ~ dnorm(0.3, psi[j])
    }  # end of j

    psd ~ dgamma(9, 4)
    sgd <- 1/psd

    phi[1:2, 1:2] ~ dwish(R[1:2, 1:2], 4)
    phx[1:2, 1:2] <- inverse(phi[1:2, 1:2])
}  # end of model
