  # WinBUGS code for Bayesian semiparametric linear SEM with covariates.
  model{
    for(i in 1:N){
      L[i] ~ dcat(pi[])
      #measurement equation
      for(j in 1:P){y[i,j]~dnorm(mu.y[i,j],psi[j])}
      mu.y[i,1]<-beta[1,1]*co1[i,1]+beta[1,2]*co1[i,2]+1.0*eta[i]+1.0*xi[i,2]
      mu.y[i,2]<-beta[2,1]*co1[i,1]+beta[2,2]*co1[i,2]+lam[1]*eta[i]+lam[2]*xi[i,2]
      mu.y[i,3]<-beta[3,1]*co1[i,1]+beta[3,2]*co1[i,2]+lam[3]*eta[i]+lam[4]*xi[i,2]
      mu.y[i,4]<-beta[4,1]*co1[i,1]+beta[4,2]*co1[i,2]+1.0*xi[i,1]
      mu.y[i,5]<-beta[5,1]*co1[i,1]+beta[5,2]*co1[i,2]+lam[5]*xi[i,1]
      mu.y[i,6]<-beta[6,1]*co1[i,1]+beta[6,2]*co1[i,2]+lam[6]*xi[i,1]
      mu.y[i,7]<-beta[7,1]*co1[i,1]+beta[7,2]*co1[i,2]+1.0*xi[i,2]
      mu.y[i,8]<-beta[8,1]*co1[i,1]+beta[8,2]*co1[i,2]+lam[7]*xi[i,2]
      mu.y[i,9]<-beta[9,1]*co1[i,1]+beta[9,2]*co1[i,2]+lam[8]*xi[i,2]
      #structural equation
      for(j in 1:2){xi[i,j]<-ZL[L[i],j]}
      eta[i]~dnorm(nu[i],psd)
      nu[i]<-gam[1]*co2[i,1]+gam[2]*xi[i,1]+gam[3]*xi[i,2]
    }# end of i
  # prior distributions related to nonparametric part
  for(k in 1:C){for(j in 1:2){ZL[k,j]~dnorm(0.0,0.1)}}
  alpha<-3.0
  # Stick-breaking prior
  pi[1]<-V[1]
  V[C]<-1
  for(j in 2:C) {pi[j]<-V[j]*(1-V[j-1])*pi[j-1]/V[j-1]}
  for(k in 1:C-1){V[k]~dbeta(1,alpha)}
  # prior distributions related to parametric part
  lam[1]~dnorm(1.5,psi[2])
  lam[2]~dnorm(1.5,psi[2])
  lam[3]~dnorm(1.5,psi[3])
  lam[4]~dnorm(1.5,psi[3])
  lam[5]~dnorm(1.5,psi[5])
  lam[6]~dnorm(1.5,psi[6])
  lam[7]~dnorm(1.5,psi[8])
  lam[8]~dnorm(1.5,psi[9])
  for(j in 1:P){beta[j,1]~dnorm(1.0,psi[j])}
  for(j in 1:P){beta[j,2]~dnorm(0.7,psi[j])}
  gam[1]~dnorm(1.0,psd)
  gam[2]~dnorm(0.2,psd)
  gam[3]~dnorm(0.7,psd)
  psd~dgamma(9.0,8.0);sgd<-1/psd
  for(j in 1:P){psi[j]~dgamma(9.0,8.0);sgm[j]<-1/psi[j]}
} #end of model